import java.util.zip.ZipFile
import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'cpp'
    id 'maven-publish'
}

group = "net.derfruhling.discord"
version = "1.0-SNAPSHOT"

def socialSdkHeaders = tasks.register("socialSdkHeaders", Copy) {
    from(zipTree("discord-social-sdk.zip")) {
        include "discord_social_sdk/include/**"

        eachFile { f ->
            f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1))
        }

        includeEmptyDirs = false
    }

    into "discord_social_sdk"
}

def socialSdkGrabLicense = tasks.register("socialSdkGrabLicense", Copy) {
    from(zipTree("discord-social-sdk.zip")) {
        include "discord_social_sdk/License-Notices.txt"

        eachFile { f ->
            f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1))
        }

        includeEmptyDirs = false
    }

    into "discord_social_sdk"
}

def socialSdkWindowsBinaries = tasks.register("socialSdkWindowsBinaries", Copy) {
    from(zipTree("discord-social-sdk.zip")) {
        include "discord_social_sdk/bin/release/discord_partner_sdk.dll"
        include "discord_social_sdk/lib/release/discord_partner_sdk.lib"

        eachFile { f ->
            f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1))
        }

        includeEmptyDirs = false
    }

    into "discord_social_sdk"
}

def socialSdkLinuxBinaries = tasks.register("socialSdkLinuxBinaries", Copy) {
    from(zipTree("discord-social-sdk.zip")) {
        include "discord_social_sdk/lib/release/libdiscord_partner_sdk.so"

        eachFile { f ->
            f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1))
        }

        includeEmptyDirs = false
    }

    into "discord_social_sdk"
}

def socialSdkMacosBinaries = tasks.register("socialSdkMacosBinaries", Copy) {
    from(zipTree("discord-social-sdk.zip")) {
        include "discord_social_sdk/lib/release/libdiscord_partner_sdk.dylib"

        eachFile { f ->
            f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1))
        }

        includeEmptyDirs = false
    }

    into "discord_social_sdk"
}

def buildDir = layout.buildDirectory.get()

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    components {
        create("socialsdk4j", NativeLibrarySpec) {
            targetPlatform "x64"

            sources.cpp {
                source {
                    srcDir 'src/socialsdk4j/cpp'
                    include '**/*.cpp', '**/*.cc'
                }
            }

            binaries.all {
                if(it instanceof StaticLibraryBinarySpec) {
                    it.buildable = false
                }

                it.tasks.configureEach {
                    it.dependsOn(socialSdkHeaders)
                }

                def jvmHome = Jvm.current().javaHome

                if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args '-I', "${jvmHome}/include"
                    cppCompiler.args '-I', "${jvmHome}/include/darwin"
                    cppCompiler.args '-mmacosx-version-min=10.13'
                    linker.args '-mmacosx-version-min=10.13'
                    linker.args '-stdlib=libc++'

                    it.tasks.configureEach {
                        it.dependsOn(socialSdkMacosBinaries)
                    }

                    cppCompiler.args "--std=c++17"
                    linker.args "-L${projectDir}/discord_social_sdk/lib/release"
                    linker.args "-ldiscord_partner_sdk"
                } else if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${jvmHome}/include"
                    cppCompiler.args '-I', "${jvmHome}/include/linux"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'

                    it.tasks.configureEach {
                        it.dependsOn(socialSdkLinuxBinaries)
                    }

                    cppCompiler.args "--std=c++17"
                    linker.args "-L${projectDir}/discord_social_sdk/lib/release"
                    linker.args "-ldiscord_partner_sdk"
                } else if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I${jvmHome}/include"
                    cppCompiler.args "-I${jvmHome}/include/win32"

                    it.tasks.configureEach {
                        it.dependsOn(socialSdkWindowsBinaries)
                    }

                    cppCompiler.args "/std:c++17"
                    linker.args "${projectDir}/discord_social_sdk/lib/release/discord_partner_sdk.lib"
                }

                cppCompiler.args "-I${projectDir}/discord_social_sdk/include"
//                cppCompiler.args "-I${buildDir}/generated/jni-headers"
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

configurations {
    artifact
}

dependencies {
    implementation platform('org.apache.logging.log4j:log4j-bom:2.24.3')
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'
    compileOnly 'org.jetbrains:annotations:26.0.2'
}

def socialSdkBinaries = tasks.register("socialSdkBinaries") {
    dependsOn(socialSdkLinuxBinaries, socialSdkMacosBinaries, socialSdkWindowsBinaries)
}

def jarLinuxBinaries = tasks.register("jarLinuxBinaries", Jar) {
    archiveClassifier = "linux"
    dependsOn(socialSdkBinaries, socialSdkGrabLicense)

    from("LICENSE.txt") {
        rename("LICENSE.txt", "LICENSE-SocialSDK4J.txt")
    }

    from("discord_social_sdk/License-Notices.txt") {
        rename("License-Notices.txt", "LICENSE-DiscordSocialSdk.txt")
    }

    from("discord_social_sdk/lib/release/libdiscord_partner_sdk.so")
    from(buildDir.file("libs/socialsdk4j/shared/libsocialsdk4j.so"))

    if(System.getProperty("ci") != "yes")
        dependsOn("socialsdk4jSharedLibrary")
}

def jarMacosBinaries = tasks.register("jarMacosBinaries", Jar) {
    archiveClassifier = "macos"
    dependsOn(socialSdkBinaries, socialSdkGrabLicense)

    from("LICENSE.txt") {
        rename("LICENSE.txt", "LICENSE-SocialSDK4J.txt")
    }

    from("discord_social_sdk/License-Notices.txt") {
        rename("License-Notices.txt", "LICENSE-DiscordSocialSdk.txt")
    }

    from("discord_social_sdk/lib/release/libdiscord_partner_sdk.dylib")
    from(buildDir.file("libs/socialsdk4j/shared/libsocialsdk4j.dylib"))

    if(System.getProperty("ci") != "yes")
        dependsOn("socialsdk4jSharedLibrary")
}

def jarWindowsBinaries = tasks.register("jarWindowsBinaries", Jar) {
    archiveClassifier = "windows"
    dependsOn(socialSdkBinaries, socialSdkGrabLicense)

    from("LICENSE.txt") {
        rename("LICENSE.txt", "LICENSE-SocialSDK4J.txt")
    }

    from("discord_social_sdk/License-Notices.txt") {
        rename("License-Notices.txt", "LICENSE-DiscordSocialSdk.txt")
    }

    from("discord_social_sdk/bin/release/discord_partner_sdk.dll")
    from(buildDir.file("libs/socialsdk4j/shared/socialsdk4j.dll"))

    if(System.getProperty("ci") != "yes")
        dependsOn("socialsdk4jSharedLibrary")
}

def generateNativeSourcesJar = tasks.register("generateNativeSourcesJar", Jar) {
    archiveClassifier = "native-sources"

    from("src/socialsdk4j/cpp") {
        include("*.cpp", "*.hpp")
    }
}

def generateDummyJar = tasks.register("generateDummyJar", Jar) {
    archiveClassifier = "dummy"

    from("DUMMY_README.md") {
        rename("DUMMY_README.md", "README.md")
    }
}

def discordPartnerSdkWindowsArtifact = artifacts.add('artifact', jarWindowsBinaries)
def discordPartnerSdkLinuxArtifact = artifacts.add('artifact', jarLinuxBinaries)
def discordPartnerSdkMacosArtifact = artifacts.add('artifact', jarMacosBinaries)

def nativeSourcesJar = artifacts.add('artifact', generateNativeSourcesJar)
def dummyJar = artifacts.add('artifact', generateDummyJar)

publishing {
    publications {
        create("maven", MavenPublication) {
            groupId = 'net.derfruhling.discord.socialsdk4j'
            artifactId = 'socialsdk4j'

            artifact(jar)
            artifact(javadocJar)
            artifact(sourcesJar)

            artifact(discordPartnerSdkLinuxArtifact) {
                classifier = "linux"
            }

            artifact(nativeSourcesJar) {
                classifier = "linux-sources"
            }

            artifact(dummyJar) {
                classifier = "linux-javadoc"
            }

            artifact(discordPartnerSdkMacosArtifact) {
                classifier = "macos"
            }

            artifact(nativeSourcesJar) {
                classifier = "macos-sources"
            }

            artifact(dummyJar) {
                classifier = "macos-javadoc"
            }

            artifact(discordPartnerSdkWindowsArtifact) {
                classifier = "windows"
            }

            artifact(nativeSourcesJar) {
                classifier = "windows-sources"
            }

            artifact(dummyJar) {
                classifier = "windows-javadoc"
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/der-fruhling-entertainment/socialsdk4j")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }

        maven {
            name = "TestDeploy"
            url = layout.buildDirectory.file("test-deploy")
        }
    }
}

tasks.test {
    useJUnitPlatform()
}
